/**
 * TextMaskPhone ( phoneNumber )
 * Formats a string of text as a North America phone number.
 *
 * @parameter phoneNumber: The phone number to format
 *
 * @return The formatted phone number text
 *
 * @uses TextMask ( text ; prototype )
 *
 * @author @jbante
 */

Let ( [
	_numeric = "0123456789" ;
	_firstDigit = Left ( Filter ( phoneNumber ; _numeric ) ; 1 ) ;
	_alphanumeric = _numeric & "ABCDEFGHIJKLMNOPQRSTUVWXYZ" ;
	_text = Filter ( Upper ( phoneNumber ) ; _alphanumeric ) ;
	_startsWithDigit = Left ( _text ; 1 ) = _firstDigit ;
	_mask =
		Case (
			Left ( _text ; 1 ) = "1" ;	"#-###-###-#### ext. *" ;
			Length ( _text ) > 7 ;	"###-###-#### ext. *" ;
			/* Else */	"###-####"
		)
] ;
	If ( _startsWithDigit ; TextMask ( _text ; _mask ) )
)